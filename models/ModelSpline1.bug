model  {  

#https://www.researchgate.net/publication/5142911_Bayesian_Analysis_for_Penalized_Spline_Regression_Using_WinBUGS
#https://www.jstatsoft.org/v14/i14/paper
#m(x, θ) = β0 + β1x + Sum(uk * |x − κk|^3)

for (i in 1:N) {  
    Hit[i] ~ dbin(p[i], AtBat[i])
    p[i] ~ dbeta(alpha[i], beta[i])
     
    alpha[i] <- max(0, min(1000, alpha0[i]))
    beta[i] <- max(0, min(1000, beta0[i]))

    alpha0[i] <- mu[i]/sigma0
    beta0[i] <- (1-mu[i])/sigma0

    mu[i] <- max(0, mu1[i])

    mu1[i] <- mu0 + muAB * log(AtBat[i]) + muHandBoth * BatsBoth[i] + muHandLeft * BatsLeft[i] + 
             inprod(betaX[],X[i,]) +
             inprod(betaZ[],Z[i,])

#computation time can be 5 to 10 times longer when inprod is used   
#inprod(betaX[],X[i,]) + inprod(betaZ[],Z[i,])
#betaX[1] * X[i,1] + betaX[2] * X[i,2] + 
#betaZ[1] * Z[i,1] + betaZ[2] * Z[i,2] + betaZ[3] * Z[i,3] + betaZ[4] * Z[i,4] + betaZ[5] * Z[i,5]
}  

#Prior distribution of the fixed effects parameters
for (l in 1:degree)
{
  betaX[l] ~ dnorm(0,1.0E-6)
}

#Prior distributions of the random effects parameters
for (k in 1:nknots)
{
  betaZ[k] ~ dnorm(0,taub)
}
taub ~ dgamma(1,1)

tau ~ dgamma(1.0,1.0) 
tau0 <- max(0.1, tau)
sigma0 <- 1 / tau0 

mu0 ~ dnorm(0,taumu0) 
taumu0 ~ dgamma(1.0,1.0) 
muAB ~ dnorm(0,taumuAB)  
taumuAB ~ dgamma(1.0,1.0) 
muHandBoth ~ dnorm(0,taumuHandBoth) 
taumuHandBoth ~ dgamma(1.0,1.0) 
muHandLeft ~ dnorm(0,taumuHandLeft) 
taumuHandLeft ~ dgamma(1.0,1.0) 
} 

 
 